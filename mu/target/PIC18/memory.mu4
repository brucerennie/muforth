( This file is part of muforth: https://muforth.dev/

  Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

loading PIC18 memory

decimal

( Define the basics of the target's memory.)

 2 constant bytes/cell
         -d little-endian

ld target/common/memory.mu4   ( generic memory image support)

( Program memory addresses are 22 bits. The PC is 21 bits, but the largest
  PIC18 parts have only 128 KiB of flash.

  I've have only compared two subfamilies - PIC18F1xK50 and the PIC18-Q -
  but it seems that there are *standard* regions defined for the flash, but
  not every chip defines all of them.

  The 1xK50, for example, defines Flash, User ID, and Config space, and
  folds the device ID and revision ID into one word at 3f_fffe. The -Q series
  defines *all* of the following:

  Flash             : 00_0000 - 1f_ffff
  User IDs          : 20_0000 - 20_001f
  Device info area  : 2c_0000 - 2c_00ff
  Config words      : 30_0000 - 30_????
  EEPROM            : 38_0000 - 38_????
  Device config info: 3c_0000 - 3c_????
  Revision ID       : 3f_fffc - 3f_fffd
  Device ID         : 3f_fffe - 3f_ffff
)

( Let's define constants for the origins of each of the Flash areas,
  whether they exist or not.)

"20_0000 constant @user-id
"2c_0000 constant @device-info    ( unique ID, calibration info)
"30_0000 constant @config
"38_0000 constant @eeprom
"3c_0000 constant @device-config  ( erase page size, number of flash pages, etc)
"3f_fffc constant @revision-id
"3f_fffe constant @device-id

( Two memory aspaces: flash/id/config/eeprom, and data [ram + i/o].)

22 make-aspace  ( flash/id/config/eeprom)
   dup #flash make-image
          dup make-region flash
              make-region boot

      dup 256 make-image  ( XXX how big is big enough?)
              make-region user-id

      dup 256 make-image  ( XXX how big is big enough?)
              make-region config

      #eeprom make-image
              make-region eeprom

( XXX Is the flash space choose-image totally chip-specific? Or can we do
  something like this for everything:)

-: ( target)  ( choose an image from an address in flash space)
   dup @user-id u< if  drop flash ^  then
   dup @config  u< if  drop user-id ^  then
       @eeprom  u< if       config ^  then
                            eeprom ;

( Install choose-image in the flash aspace.)
flash 'choose-image !

( Since on some PIC18 families the ram starts at a non-zero offset into
  data space - specifically, at @ram - we want to make sure that the ram
  region - from @ram to @ram #ram + - fits into the ram image, which needs to
  be a power of 2 in size.

  The easiest way to do this is to take the larger of @ram and #ram - we
  assume they are both powers of 2! - and double it.)

( For the data aspace we keep the default of "drop" for choose-image.)
16 make-aspace  ( data: ram + i/o)
   @ram #ram max 2* make-image  ( ensure that [@ram #ram +] is within the ram image)
                    make-region ram


.ifdef legacy-pic-memory

( NOTE: Cribbed from AVR)

( Space for compiling into. PIC18's can get big; for now let's limit
  ourselves to 128k address space. Since the program space is
  word-addressed, this means 64k words.

  XXX: We should really define this in a device/xxx file that gets loaded
  _before_ this one.)

( Memory images should be stored in the dictionary like strings: prefixed
  by their length. When we push an image, we push the address of its first
  byte, but the previous cell countains the count, if we want to do bounds
  checking or address wrapping.)

variable image  ( image buffer pointer; points to either flash- or eeprom-image)

: make-image  create  ( #bytes)  aligned  dup ,  allot
              does>  cell+ ( skip count)  image ! ;

( Two real images)
128 Ki make-image  flash-image
  4 Ki make-image  eeprom-image  ( XXX prob way too big for PIC18)

( And a dummy image)
16 make-image  data-image  ( so "da" when not chatting will show something)


: #image  image @  cell- @  ( get byte count) ;
: 'image  image @           ( get address) ;

: wrap   #image 1-  and ;
: +a  ( a1 a2 - sum)  +  wrap ;  ( add and wrap to image boundary)

: image+  ( target - host)  wrap  'image + ;
: image-  ( host - target)        'image -  wrap ;


( This is a bit of a hack, but we have to do it somehow. PIC18's have, in
  addition to the program memory, two small regions of flash memory: one
  for storing ID bytes, and one for CONFIG bytes. The ID space is 8 bytes
  long; the size of the CONFIG space varies from part to part, and PIC18s
  have all manner of different CONFIGxx registers. Some of the newer
  USB-capable chips, such as the 26J53, have only 8 bytes of CONFIG
  registers.

  But the 1xK50 and 2xK50 have up to 14 bytes of CONFIG space. So let's
  allocate 16 bytes. Hopefully that will be enough!

  Rather than making these images in their own right - which engenders its
  own set of problems - lets create the idea of flash-spaces. These are
  self-indexing, and also "know" where they begin.)

: flash-space
   create  ( #bytes origin)  , ( origin)  aligned  dup , ( #bytes)  allot
   does>  ( target 'space - host | 0)
      @+ push ( target origin)  -  ( offset)  pop @+ push ( #bytes)
      over swap u< if  pop +  ^  then  ( offset)  pop 2drop  0 ;

( Ok, let's create the two spaces and make each one 16 bytes long so that
  reads by the memory dumping code will work reasonably.)

16 "20_0000 flash-space     id-space
16 "30_0000 flash-space config-space
16               buffer   dead-space  -1 dup dead-space 2!  ( fill with ff)


( DEBUG: Define show-comma to show code as it's compiled.)
.ifdef show-comma
: .debug-comma  dup  .h8_ ;
.else
: .debug-comma ;
.then

( DEBUG: Define show-c! to show writes to image.)
.ifdef show-c!
: .debug-c!   cr  ( byte addr)  2dup .hcell_  .h8 ;
: .debug-w!   cr  ( word addr)  2dup .hcell_  .hcell ;
.else
: .debug-c! ;
: .debug-w! ;
.then

: image-c@             image+ c@ ;
: image-c!  .debug-c!  image+ c! ;

( PIC18s are little-endian.)
: image-@  ( a - w)           image+  leh@ ;
: image-!  ( w a)  .debug-w!  image+  leh! ;


defer >prog ( dump program memory)
defer >data ( dump data memory)
defer >ee   ( dump eeprom memory)

: log2  ( n - #bits)
   0 swap  begin  dup 1 and 0= while  1 u+  u2/  repeat  drop ;

: addrbits  ( size)  log2  |addr ! ;

-: ( buf a u)  swap image+ -rot  cmove ;
: >image-read   [ #]  is |read ;  ( read from current image buffer)

: choose-flash-space  ( target - host)
   dup #flash u< if  image+  ^  then  ( normal flash-image access)
   dup     id-space  =if  nip ^  then  drop
   dup config-space  =if  nip ^  then  2drop  dead-space ;

-: ( buf a u)  swap choose-flash-space  -rot cmove ;
: >flash-read   [ #]  is |read ;  ( read from flash/id/config buffer)

: >image-prog   >flash-read                   4 Mi addrbits ;
: >image-data   >image-read    data-image     4 Ki addrbits ;
: >image-ee     >image-read  eeprom-image  #eeprom addrbits ;

: >image
   ['] leh@  is |cell@
              2 |cell !  ( size of cell in bytes)

   ['] >image-prog  is >prog
   ['] >image-data  is >data
   ['] >image-ee    is >ee

   >prog  ( default) ;

>image

( Dictionary operators.)
variable 'ram     ( ram pointer)
:  ram  'ram @ ;
: +ram  'ram +! ;

variable 'h   ( image dictionary pointer pointer; points to one of:)
   2variable 'app     ( application flash pointer, origin)
   2variable 'boot    ( bootloader flash pointer, origin)
   2variable 'ee      ( eeprom pointer, origin)
   2variable 'sig     ( signature pointer, origin)

: app     'app     'h !   flash-image  ;
: boot    'boot    'h !   flash-image  ;
: signature  'sig  'h !   flash-image  ;
: eeprom  'ee      'h !   eeprom-image ;

: h   'h @ ;  ( get _address_ of current dictionary pointer)


( XXX A better name for this? I want origin to return the origin of a
  space, not set the current space pointer to something...)

: org    ( a) ( set dict pointer)  h ! ;

meta
: here    ( - a)  h        @ ;
: origin  ( - a)  h cell+  @ ;

: aligned  1+  -2 and ;
: align    \m here \m aligned  org ;
: allot ( n)    h +! ;

: c,        ( b)                 \m here image-c!  1 \m allot ;
: align,    ( b)   \m here 1 and  if  \m c,  ^  then  drop ;
: ,         ( w)    0 \m align,  \m here image-!   2 \m allot ;

2 constant cell
: cells  2* ;
: cell/  2/ ;
: cell+  \m cell + ;
: cell-  \m cell - ;

forth
( return the origin & number of bytes in current region)
: region  ( - a u)  h 2@  ( origin ptr)  over - ;

( Initialization.)
: fferase  'image #image "ff fill ;  ( erases _current_ image)
: wipe
   "20_0000 id-space 16 "ff fill
   "30_0000 config-space 16 "ff fill
   data-image  "d0d0_dead_d0d0_dead dup  'image 2!
   eeprom-image fferase
   flash-image fferase ;  ( leaves flash-image as _current_ image)

: app!      ( app-origin)  dup 'app   2!  ( set ptr and origin to origin) ;
: boot!    ( boot-origin)  dup 'boot  2!  ( set ptr and origin to origin) ;
: ee!    ( eeprom-origin)  dup 'ee    2!  ( set ptr and origin to origin) ;
: sig!      ( sig-origin)  dup 'sig   2!  ( set ptr and origin to origin) ;

.then  ( ifdef legacy)
