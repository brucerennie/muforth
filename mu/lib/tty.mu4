( This file is part of muforth: https://muforth.dev/

  Copyright 2002-2023 David Frech. (Read the LICENSE for details.)

( We need these words to make dumping memory, decompiling code, and
  disassembling more beautiful. All console i/o is done in raw mode.

  modify-termios is also useful for setting the termios of the target
  serial link.)

( Open the tty device. We assume that we are running interactively!)
z" /dev/tty" open-file-rw  constant tty

( Make it easier to modify termios settings.)
: modify-termios  ( xn .. x0 'cfa fd - yn .. y0)
   push ( 'cfa)
   r@ ( fd)  here get-termios drop
             here swap @execute
   pop ( fd) here set-termios ;

( user-termios will create a word that runs a certain modification against
  the user's console termios.)

: user-termios   create  ( cfa)  ,  does>  tty  modify-termios ;

( Set up a raw mode tailored for human interaction.)
' set-termios-user-raw   user-termios user-raw

: <key  ( fd - ch)
   charbuf 1 read
   if  charbuf c@ ^  then  ( read a character successfully)
   error" <key read 0 chars" ;

( Read from tty - not stdin!)
: key  tty  <key ;
